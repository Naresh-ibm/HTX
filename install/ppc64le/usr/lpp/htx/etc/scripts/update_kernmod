#!/bin/awk -f

# @(#)95   1.8  src/htx/usr/lpp/htx/etc/scripts/update_kernmod, htxconf, htxubuntu 12/17/08 00:40:10 

# This script updates the HTX kernel modules
# It removes HTX kernel modules which are not used by HTX
# but are loaded by default while login as user htx
# It also loads those HTX kernel modules which are required
# but somehow are unloaded in previous run.


BEGIN {
    pdiagex = 0
    hxehvdd = 0
}

/HE_name/ {
    if($3 ~ /hxeknox_st/)   { pdiagex=1 }
    if($3 ~ /hxegoliadx/)   { pdiagex=1 }
    if($3 ~ /hxescurry/)    { pdiagex=1 }
    if($3 ~ /hxeHV/)        { hxehvdd=1 }
    if($3 ~ /hxeohc/)       { pdiagex=1 }
    if($3 ~ /hxesanjcto/)   { pdiagex=1 }
    if($3 ~ /hxelai/)       { pdiagex=1 }
    if($3 ~ /hxes2kentx/)   { pdiagex=1 }
    if($3 ~ /hxevicentx/)   { pdiagex=1 }
    if($3 ~ /hxedtx/)       { dmax=1  }
    if($3 ~ /hxecmem/)      { axonram=1 }
    if($3 ~ /hxeaxon/)      { axon_util=1 }
    if($3 ~ /hxembx/)       { mailbox=1  }
    if($3 ~ /hxemsic/)      { msic=1 }

}

END {
    print "Updating HTX kernel modules..."

    update_kernmod("pdiagex",pdiagex);
    update_kernmod("hxehvdd",hxehvdd)
    update_kernmod("dmax",dmax)
    update_kernmod("axonram",axonram)
    update_kernmod("axon_util",axon_util)
    update_kernmod("mailbox",mailbox)
    update_kernmod("msic",msic)
}

function update_kernmod(mod_name,use_mod) {

    cmd=sprintf("lsmod | grep %s",mod_name);
    loaded=snarf(cmd)
    if(loaded != "" && use_mod == 0)
    {
        cmd=sprintf("rmmod %s",mod_name);
        system(cmd)
    }
    if(loaded == "" && use_mod == 1)
    {
        cmd=sprintf("insmod -f /usr/lpp/htx/etc/kernext/%s.* >>/tmp/htxStartup.log",mod_name)
        system(cmd)
    }
}

function snarf(command) {
     snarf_input=""
     command | getline snarf_input
     close(command)
     return snarf_input
}

